\chapter{Introduction to RISC V and runtime.s}

\section{Choosing a Simulator}

In case of MIPS, it was straight forward to compile and run the assembly code but in case of RISC V, I had to struggle for some time as I couldn't find an appropriate documentation for it. After many iterations, I found the most suitable simulator, namely, \href{https://github.com/TheThirdOne/rars}{Rars}.

Please see the corresponding description of system calls \href{https://github.com/TheThirdOne/rars/wiki/Environment-Calls}{here}. It as well have a nice companion \href{https://github.com/TheThirdOne/rars/wiki}{documentation} which I expect one to read before proceeding further with the report.

There are other ways to compile and run RISC V code:

\begin{itemize}

  \item \href{http://www.kvakil.me/venus/}{Venus}, \href{https://github.com/kvakil/venus}{Github repo}. Note that for system calls, their argument register is different, see \href{https://github.com/TheThirdOne/rars/issues/45}{this}. 

  \item \texttt{spike} (sort of an official simulator), installed when using \texttt{riscv-gnu-toolchain}. Note it was as well required to install \href{https://github.com/riscv/riscv-pk}{pk}. System calls are different than RARS, basically it follows linux system calls. Can see these system calls \href{https://github.com/riscv/riscv-pk/blob/master/pk/syscall.c}{here1} and \href{https://github.com/riscv/riscv-pk/blob/master/pk/syscall.h}{here2}. And linux system calls \href{http://man7.org/linux/man-pages/man2/syscalls.2.html}{here}, note that system calls of interest can be concisely seen \href{https://rv8.io/syscalls.html}{here}.
  
  So to compile and run the program, do: (Don't know if this is the intended way but after a lot of trial and error, I found this)
  
  \begin{minted}{bash}
  riscv64-unknown-elf-as -o filename.o filename.s 
  riscv64-unknown-elf-ld -o filename filename.o 
  spike pk filename
  \end{minted}

\end{itemize}

\section{Basic Examples}

Few things to keep in mind:

\begin{itemize}
  \item Note that dont use \texttt{\$reg}, instead simply use \texttt{reg}.
  \item Always have two sections, one for data and another for text.
  \item During \texttt{ecall} all registers besides the output are guaranteed not to change.
  \item Put return value in \texttt{a0}.
  \item When we want our register value to be saved across system call, we can simply put it in stack instead of using registers $s_i$ as anyway we would have to save their value first in stack so no change in overhead.
  \item \texttt{a7} is used to tell which system call.
  \item Save \texttt{ra} register if executing \texttt{jal} inside a function.
\end{itemize}

\subsubsection{Hello World}

\begin{minted}{asm}
.data # Tell the assembler we are defining data not code

msg: # Label this position in memory so it can be referred to in our code
  .string "hello world" # Copy the string "hello world" into memory 

.text # Tell the assembler that we are writing code (text) now 

start:
  li a7, 4 # li means to Load Immediate and we want to load the value 4 into register a7
  la a0, msg  # la is similar to li, but works for loading addresses
  ecall
  li a7, 10  # Exit call
  ecall
\end{minted}

To get the same code working using spike. 

\begin{minted}{asm}
.globl _start # We must need to give _start, .globl helps to see it outside this file
.data 
str:   
  .string "Hello World!\n" 

.text 
_start: 

  li a0, 1   
  la a1, str 
  li a2, 13  # length of the string as required for linux system call. We can write a function which will determine the length of the string by checking for terminating null character.
  li a7, 64  
  ecall 

  li a0, 0   # The exit code we will be returning is 0
  li a7, 93  # Again we need to indicate what system call we are making and this time we are calling exit(93)
  ecall 
\end{minted}

\subsubsection{Saving callee save registers}
\begin{minted}{asm}
.data

  bef: .string "Before modification, value is: "
  dur: .string "\nInside function, value is: "
  aft: .string "\nAfter function call, value is: "

.text 

main:
  addi s0, zero, 1
  # Print bef
  la a0, bef
  li a7, 4
  ecall
  # Print int
  li a7, 1
  mv a0, s0
  ecall
  jal increment
  # Print aft
  la a0, aft
  li a7, 4
  ecall
  # Print int
  li a7, 1
  mv a0, s0
  ecall
  # Exit
  li a7, 10
  ecall



increment:
  addi sp, sp, -4
  sw s0, 0(sp) # '0' denotes the offset, in case of 0, we can simply omit it.
  addi s0, s0, 1
  # Print string
  la a0, dur
  li a7, 4
  ecall
  # Print the incremented integer
  mv a0, s0
  li a7, 1
  ecall
  lw s0, 0(sp)
  addi sp, sp, 4
  jr ra
\end{minted}

\section{runtime.s}

There are some standard functions which our Tiger program can use. They are written in \texttt{runtime.s}. I could have as well used \href{https://www.cs.princeton.edu/~appel/modern/spim/runtime.s}{file} provided by author but as I want to augment it further and implement things my way, I decided to write runtime myself. Each of the functions of runtime is explained below along with code for easy understanding. 

\begin{minted}{asm}
.data 

__exitMessage: .string "Exited with code: "
__newLine: .string "\n"

.text

# Many of the below written functions assume that the given input is correct. 

# Given the exit code (in a0 ofc), terminate with that exit code.
exit:
    mv t0, a0
    # Print exit message
    la a0, __exitMessage
    li a7, 4 
    ecall 
    # Print code
    mv a0, t0 
    li a7, 1 
    ecall 
    # Print new line
    la a0, __newLine 
    li a7, 4 
    ecall  
    # Exit
    li a7, 10
    ecall

# Not of non zero integer is 0 whereas not of 0 is 1.
not:
    beqz a0, retOne
    li a0, 0
    jr ra 
    retOne:
        li a0, 1
        jr ra 

# Given the string s in a0, return the number of characters in it.
# This is aswell needed for string concatenation.
size:
    mv t0, a0
    mv a0, zero
    sizeLoop:
        lb t1, (t0)
        beqz t1, sizeExit
        addi a0, a0, 1
        addi t0, t0, 1
        j sizeLoop
    sizeExit:
        jr ra

# Copy the string completely (i.e. including zero / null character) whose address is at a1, to the address starting at a0, returning the address of the last character of copied string.
stringCopy:
    stringCopyLoop:
        lb t0, (a1)
        sb t0, (a0)
        beqz t0, stringCopyExit
        addi a0, a0, 1
        addi a1, a1, 1
        j stringCopyLoop
    stringCopyExit:
        jr ra

# Concatenate str1 present in a0 with str2 present in a1.
concat:
    addi sp, sp, -12
    sw a0, (sp)
    sw a1, 4(sp)
    sw ra, 8(sp)
    jal size
    li t0, 1  # Will contain len(str1) + len(str2) + 1. '+1' for null character.
    add t0, t0, a0
    addi sp, sp, -4
    sw t0, (sp)
    lw a0, 8(sp)  # offset is changed
    jal size
    lw t0, (sp)
    add t0, t0, a0
    addi sp, sp, 4
    mv a0, t0
    li a7, 9
    ecall
    addi sp, sp, -4
    sw a0, (sp)
    lw a1, 4(sp)
    jal stringCopy
    lw a1, 8(sp)
    jal stringCopy
    lw a0, (sp)
    lw ra, 12(sp)
    addi sp, sp, 16
    jr ra

# "function substring (s: string, first : int, n : int) : string" Substring of string s, starting with character first, n characters long.
# Hoping that given input is valid.
substring:
    # Allocate space
    mv a3, a0  # saving a0
    mv a0, a2
    addi a0, a0, 1  # for null character 
    li a7, 9
    ecall
    # making a3 point to the desired substring
    add a3, a3, a1 
    mv t0, a0  # we need to return this
    substringLoop:
        lb t1, (a3) 
        sb t1, (a0)
        beqz a2, substringExit
        addi a0, a0, 1 
        addi a3, a3, 1
        addi a2, a2, -1
        j substringLoop
    substringExit: 
        mv a0, t0
        jr ra

# str1 > str2 ?
stringGreat:
    stringGreatLoop:
        lb a2 (a0)
        lb a3 (a1)
        bgt a2, a3  stringGreatA
        blt a2, a3  stringGreatB
        # If we have reached this point that means both are equal and if one of them is zero that means other is aswell 0, so in case strings are equal, I must return 0.
        beqz a2, stringGreatB
        addi a0, a0, 1
        addi a1, a1, 1
        j stringGreatLoop
    stringGreatA:
        li a0, 1
        jr ra
    stringGreatB:
        li a0, 0 
        jr ra 
    
# str1 < str2 ?
stringLess:
    stringLessLoop:
        lb a2 (a0)
        lb a3 (a1)
        blt a2, a3  stringLessA
        bgt a2, a3  stringLessB
        # If we have reached this point that means both are equal and if one of them is zero that means other is aswell 0, so in case strings are equal, I must return 0.
        beqz a2, stringLessB
        addi a0, a0, 1
        addi a1, a1, 1
        j stringLessLoop
    stringLessA:
        li a0, 1
        jr ra
    stringLessB:
        li a0, 0 
        jr ra 

# str1 == str2 ?
stringEqual:
    addi sp, sp, -12
    sw a0, (sp)
    sw a1, 4(sp)
    sw ra, 8(sp)
    jal stringGreat
    bnez a0, stringEqualExit
    lw a0, (sp)
    lw a1, 4(sp)
    jal stringLess
    bnez a0, stringEqualExit 
    li a0, 1 
    lw ra, 8(sp)
    addi sp, sp, 12 
    jr ra 
    stringEqualExit:
        li a0, 0    
        lw ra, 8(sp)
        addi sp, sp, 12 
        jr ra 


# Single-character string from ASCII value given in a0; halt program if a0 out of range.
chr:
    # Handling the error part 
    addi t0, zero, 127  
    bgt a0, t0, chrError
    bltz a0, chrError
    # Allocating
    mv t0, a0 
    li a0, 2 
    li a7, 9 
    ecall 
    # Putting the character
    sb t0 (a0)
    sb zero 1(a0)
    jr ra
    chrError:
        addi a0, zero, -1
        j exit

# Given a string in a0, return ASCII value of the first character of it, return -1 if the string is empty.
ord:
    lb t0, (a0)
    beqz t0, ordEmpty
    mv a0, t0 
    jr ra 
    ordEmpty:
        li a0, -1 
        jr ra 

# Read a character from standard input and return it as a string; return empty string on end of file.
getchar:
    # Allocate space
    li a0, 2
    li a7, 9
    ecall
    sb zero, 1(a0)  # Null character 
    # Read the character 
    mv t0, a0 
    li a7, 12 
    ecall 
    sb a0, (t0)  # Store the character 
    mv a0, t0 
    jr ra

# Absolete as of now
flush:
    jr ra

# Print the string whose address is in a0
print:
    li a7, 4
    ecall
    jr ra

# printInt:
#     # Examples in book do complex computation to print an integer, here I am putting an inbuilt function
#     # Print the integer in a0
#     li a7, 1
#     ecall
#     jr ra

# a0 contains the number of bytes we need to allocate. So, multiply it by 4 and allocate that much space from heap (system call 9). Return value is in a0 which tells the address to the allocated block (lower address value) and remember that in going downwards address decreases. Rest of the code is easy to follow. Note that a1 contains the value to which we need to initialize our array.
initArray:
  li t0, 4
  mul a0, a0, t0
    mv t1, a0
  li a7, 9
  ecall
  mv t0, a0
  add t1, t1, t0
  initArrayLoop:
        sw a1, (t0)
        addi t0, t0, 4
        beq t0, t1, initArrayExit
        j initArrayLoop
    initArrayExit:
        jr ra

# Very similar to initArray
# We just need to allocate memory, no need to initialize it with 0.
allocRecord:
    li t0, 4
    mul a0, a0, t0
    li a7, 9
    ecall
    jr ra
\end{minted}